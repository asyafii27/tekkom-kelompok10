statements  : NEWLINE* statement (NEWLINE+ statement)* NEWLINE*

statement		: KEYWORD:BALIK expr?
						: KEYWORD:LANJUT
						: KEYWORD:MANDEK
						: expr

expr        : KEYWORD:MISAL IDENTIFIER EQ expr
            : comp-expr ((KEYWORD:AND|KEYWORD:OR) comp-expr)*

comp-expr   : NOT comp-expr
            : arith-expr ((EE|LT|GT|LTE|GTE) arith-expr)*

arith-expr  :	term ((PLUS|MINUS) term)*

term        : factor ((MUL|DIV) factor)*

factor      : (PLUS|MINUS) factor
            : power

power       : call (POW factor)*

call        : atom (LPAREN (expr (COMMA expr)*)? RPAREN)?

atom        : INT|FLOAT|STRING|IDENTIFIER
            : LPAREN expr RPAREN
            : list-expr
            : if-expr
            : for-expr
            : while-expr
            : func-def

list-expr   : LSQUARE (expr (COMMA expr)*)? RSQUARE

if-expr     : KEYWORD:YEN expr KEYWORD:DADI
              (statement if-expr-b|if-expr-c?)
            | (NEWLINE statements KEYWORD:AKHIR|if-expr-b|if-expr-c)

if-expr-b   : KEYWORD:YENLIYANE expr KEYWORD:DADI
              (statement if-expr-b|if-expr-c?)
            | (NEWLINE statements KEYWORD:AKHIR|if-expr-b|if-expr-c)

if-expr-c   : KEYWORD:LIYANE
              statement
            | (NEWLINE statements KEYWORD:AKHIR)

for-expr    : KEYWORD:KANGGO IDENTIFIER EQ expr KEYWORD:NGANTI expr 
              (KEYWORD:NJANGKAH expr)? KEYWORD:DADI
              statement
            | (NEWLINE statements KEYWORD:AKHIR)

while-expr  : KEYWORD:NALIKA expr KEYWORD:DADI
              statement
            | (NEWLINE statements KEYWORD:AKHIR)

func-def    : KEYWORD:MIGUNANI IDENTIFIER?
              LPAREN (IDENTIFIER (COMMA IDENTIFIER)*)? RPAREN
              (ARROW expr)
            | (NEWLINE statements KEYWORD:AKHIR)